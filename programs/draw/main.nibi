(use "board")

(use "io")

(:= prompt "cmd ➤ ")

(fn get_valid_int [int_prompt] 
  (u64 (io::prompt int_prompt io::get::int)))

(fn get_coords [] [
  (:= result [])
  (|< result (get_valid_int "x: "))
  (|< result (get_valid_int "y: "))
  (<- result)
])

(fn cmd_draw_line [] [
  (:= start_point (get_coords))
  (:= end_point (get_coords))

  (board::set_tile ".")
  (board::draw_line start_point end_point)
])

(fn cmd_draw_square [] [

  (:= pos (get_coords))
  (:= size (get_valid_int "size: "))
  (board::set_tile "◼")
  (board::draw_square 
    (at pos 0)
    (at pos 1)
    size)
])

(fn cmd_draw_triangle [] [

  (:= p0 (get_coords))
  (:= p1 (get_coords))
  (:= p2 (get_coords))

  (board::set_tile "*")
  (board::draw_triangle p0 p1 p2)
])

(fn cmd_help [] [
  (board::clear)

  (io::println "Commands:")
  (io::println "  help - show this help")
  (io::println "  line - draw a line")
  (io::println "  square - draw a square")
  (io::println "  clear - clear the board")
  (io::println "  exit - exit the program")

  (io::prompt "Press enter to continue" io::get::str)
])

(fn cmd_clear_board [] [
  (board::set_tile " ")
  (board::fill)
])

(fn main [] [
  (board::set_tile " ")
  (board::toggle_row_col true)
  (board::init 30 30)
  (:= show_invalid_command false)
  (loop (nop) (eq true true) (nop) [
    (board::refresh)
    (if (eq true show_invalid_command) [
      (io::println "Invalid command - use `help` for more info")
      (set show_invalid_command false)
    ])
    (:= cmd (io::prompt prompt io::get::str) )
    (if (eq "help" cmd) (cmd_help))
    (if (eq "line" cmd) (cmd_draw_line))
    (if (eq "square" cmd) (cmd_draw_square))
    (if (eq "triangle" cmd) (cmd_draw_triangle))
    (if (eq "clear" cmd) (cmd_clear_board))
    (if (eq "exit" cmd) (exit 0))
  ])
])

(main)
